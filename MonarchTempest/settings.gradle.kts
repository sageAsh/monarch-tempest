/*
 * Copyright (c) 2026 American Printing House for the Blind
 * Use of this source code is governed by an MIT-style license that can be found in the LICENSE file.
 */

pluginManagement {
    repositories {
        google {
            content {
                includeGroupByRegex("com\\.android.*")
                includeGroupByRegex("com\\.google.*")
                includeGroupByRegex("androidx.*")
            }
        }
        mavenCentral()
        gradlePluginPortal()
    }
}

//Load properties from keystore.properties
var token = ""
val propertyKey = "gitlab_maven_repo_deployToken"
val keystorePropertyFile = file("keystore.properties")
if (keystorePropertyFile.exists()){
    val keystoreProperties = java.util.Properties()
    keystorePropertyFile.inputStream().use {
        keystoreProperties.load(it)
    }

    if (keystoreProperties.containsKey(propertyKey)) {
        token = keystoreProperties.getProperty(propertyKey)
    }
}

val gitlabMavenRepoUrl: String by settings
dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
    repositories {
        google()
        mavenCentral()
        maven {
            url = uri(gitlabMavenRepoUrl)
            credentials(HttpHeaderCredentials::class) {
                name = "Deploy-Token"
                value = token
            }
            authentication {
                create<HttpHeaderAuthentication>("header")
            }
        }
    }
}

rootProject.name = "Monarch Tempest"
include(":app")
 
